buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:0.10.+"
        classpath "com.jakewharton.sdkmanager:gradle-plugin:0.10.+"
    }
}

apply plugin: "android-sdk-manager"
apply plugin: "android-library"
apply plugin: "maven"

android {
    compileSdkVersion 19
    buildToolsVersion "19.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 19
    }
}

task cloneSdk << {
    exec {
        commandLine "git", "clone", "git://git.videolan.org/vlc-ports/android.git", "vlc-android"
    }
    exec {
        workingDir file("vlc-android")
        commandLine "git", "checkout", String.format("tags/%s", mavenProperty("version"))
    }
}

cloneSdk.onlyIf {
    !file("vlc-android").exists()
}

task compileSdk(dependsOn: cloneSdk) << {
    exec {
        workingDir file("vlc-android")
        commandLine "sh", "compile.sh", "--fetch"
    }
    exec {
        workingDir file("vlc-android/vlc")
        commandLine "git", "apply", "--index", file("patch/always-read-avi-index.patch").path
    }
    exec {
        workingDir file("vlc-android")
        commandLine "sh", "compile.sh", "--build", "release"
    }
    exec {
        workingDir file("vlc-android")
        commandLine "make", ".sdk"
    }
}

task buildSdk(dependsOn: compileSdk) << {
    copy {
        from "vlc-android/vlc-android/libs"
        into "src/main/jniLibs"
    }
    copy {
        from "vlc-android/vlc-android/src/org/videolan/libvlc"
        into "src/main/java/org/videolan/libvlc"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository url: String.format("file://%s", mavenLocalPath())

            pom {
                groupId = mavenProperty("group")
                artifactId = mavenProperty("artifact")
                version = mavenProperty("version")
            }
        }
    }
}

def mavenLocalPath() {
    def homePath = System.getProperty("user.home")
    def mavenPath = ".m2/repository"

    return new File(homePath, mavenPath).absolutePath
}

def mavenProperty(property) {
    def mavenFile = file("maven.properties")
    def mavenProperties = new Properties()

    mavenProperties.load(mavenFile.newInputStream())

    return mavenProperties[property]
}
